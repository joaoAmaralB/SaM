Programa → DeclLista

DeclLista → Decl DeclLista | ε

Decl → VarDecl | FuncDecl

VarDecl → Tipo ID OpcionalArray ;

Tipo → INT_TYPE | FLOAT_TYPE | STR_TYPE

OpcionalArray → LSQUARE INT RSQUARE | ε

FuncDecl → DEF ID LPAREN Parametros RPAREN Bloco

Parametros → ParamList | ε

ParamList → Param ParamListCont

ParamListCont → COMMA Param ParamListCont | ε

Param → Tipo ID OpcionalArray

Bloco → { CmdLista }

CmdLista → Cmd CmdLista | ε

Cmd → VarDecl
     | CmdID
     | IfCmd
     | WhileCmd
     | DoWhileCmd
     | ReturnCmd
     | PrintCmd

CmdID → ID CmdIDSufixo

CmdIDSufixo → IndiceOpcional ASSIGN Expr ;
            | LPAREN Argumentos RPAREN ;

IndiceOpcional → LSQUARE Expr RSQUARE | ε

IfCmd → IF Expr Bloco ElseParte

ElseParte → ELSE Bloco | ε

WhileCmd → WHILE Expr Bloco

DoWhileCmd → DO Bloco WHILE Expr ;

ReturnCmd → RETURN ReturnSufixo

ReturnSufixo → Expr ; | ;

PrintCmd → PRINT LPAREN Expr RPAREN ;

Argumentos → ArgList | ε

ArgList → Expr ArgListCont

ArgListCont → COMMA Expr ArgListCont | ε

Expr → LogicalOr

LogicalOr → LogicalAnd LogicalOrTail

LogicalOrTail → OR LogicalAnd LogicalOrTail | ε

LogicalAnd → UnaryNot LogicalAndTail

LogicalAndTail → AND UnaryNot LogicalAndTail | ε

UnaryNot → NOT Comparison | Comparison

Comparison → Additive ComparisonTail

ComparisonTail → RelOp Additive | ε

RelOp → EQ | NEQ | LT | LTE | GT | GTE

Additive → Multiplicative AdditiveTail

AdditiveTail → AddOp Multiplicative AdditiveTail | ε

AddOp → PLUS | MINUS

Multiplicative → Primary MultiplicativeTail

MultiplicativeTail → MulOp Primary MultiplicativeTail | ε

MulOp → TIMES | DIVIDE | MOD

Primary → INT
        | FLOAT
        | STRING
        | ID Postfix
        | LPAREN Expr RPAREN

Postfix → Indice | FunctionCall | ε

Indice → LSQUARE Expr RSQUARE

FunctionCall → LPAREN Argumentos RPAREN
